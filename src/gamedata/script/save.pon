# セーブ画面
*save_start
;tempsave num: 0
;historyopt enabled: false
;lockscreenshot
;rclick enabled: false
;backlay

---
if (tv.system == null) tv.system = {};
tv.system.save = {
  // 背景画像
  bg: "image/save.png",

  // ボタン用レイヤ
  btnLay: tv.layer.save + 1,
  // テキスト用レイヤ
  textLay: tv.layer.save + 2,

  // トランジションの時間
  transTime: 200,

  // [内部処理用] セーブ番号
  savePageNum: 0,
};
---

#=========================================================================================
# 描画処理
#=========================================================================================
*draw

;currentpage page: "back"

# 背景の設定
;image {
  lay: "&tv.layer.save",
  file: "&tv.system.save.bg",
  visible: false,
  x: 0,
  y: 0
}
;layopt lay: "&tv.layer.save", visible: true

# ボタン用レイヤの設定
;layopt lay: "&tv.system.save.btnLay", x: 0, y: 0, width: 1280, height: 720, visible: true
;mesopt lay: "&tv.system.save.btnLay", color: 0x000000
;clear lay: "&tv.system.save.btnLay"
;clearbuttons lay: "&tv.system.save.btnLay"

# テキスト用レイヤの設定
;layopt lay: "&tv.system.save.textLay", x:0, y: 0, width: 1280, height: 720, visible: true
;meslay lay: "&tv.system.save.textLay"
;clear lay: "&tv.system.save.textLay"
;mesopt {
  fontfamily: ["GenShinGothic", "monospace"],
  fontsize: 18,
  lineheight: 18,
  align: "left",
  color: 0x000000,
  autoreturn: false,
  margint: 0, marginr: 0, marginb: 0, marginl: 0
}

# ボタンをロック
;lockbuttons page: "fore"
;lockbuttons page: "back"
# システムボタンをロック
;locksystembuttons page: "fore"
;locksystembuttons page: "back"

# セーブボタンの設置
;nowait
;for loops: 6, indexvar: "i"
  ---
  tv.pos = [
    [60, 140], [670, 140],
    [60, 315], [670, 315],
    [60, 490], [670, 490],
  ];
  tv.x = tv.pos[tv.i][0];
  tv.y = tv.pos[tv.i][1];
  tv.saveNum = (tv.system.save.savePageNum * 6) + tv.i;
  tv.saveInfo = ponkan.getSaveDataInfo(tv.saveNum);
  tv.text = tv.saveInfo.text.replace(/\n/g, "");
  if (tv.text.length > 42) {
    tv.text = tv.text.substring(0, 42) + "...";
  }
  tv.lineList = ponkan.splitStrByLength(tv.text, 15);
  if (tv.lineList[0] == null) { tv.lineList[0] = ""; }
  if (tv.lineList[1] == null) { tv.lineList[1] = ""; }
  if (tv.lineList[2] == null) { tv.lineList[2] = ""; }
  tv.exp = "tv.clickedSaveNum = " + tv.saveNum + ";";
  tv.deleteExp = "tv.clickedDeleteNum = " + tv.saveNum + ";";
  ---
  ;textbutton {
    lay:    "&tv.system.save.btnLay",
    text:   "",
    x:      "&tv.x",
    y:      "&tv.y",
    width:  550,
    height: 150,
    bgcolors: ["0xFFFFFF", "0xFFFFFF", "0xFFFFFF"],
    bgalphas: [0.75, 0.85, 0.9],
    exp:    "&tv.exp",
    jump:   true,
    label:  "on_click_save_button"
  }
  ;childimage {
    lay:  "&tv.system.save.btnLay",
    file: "&tv.saveInfo.screenShot",
    x:    "&tv.x + 10",
    y:    "&tv.y + 10",
    alpha: 1
  }
  ;mesopt fontsize: 14, lineheight: 14, color: 0x000000
  ;locate x: "&tv.x + 251", y: "&tv.y + 10"
  = "No." + (tv.saveNum + 1)
  ;locate x: "&tv.x + 251 + 50", y: "&tv.y + 10"
  = tv.saveInfo.date
  ;mesopt fontsize: 16, lineheight: 16, color: 0x000000
  ;locate x: "&tv.x + 251", y: "&tv.y + 44"
  = tv.saveInfo.comment
  ;locate x: "&tv.x + 251", y: "&tv.y + 78"
  = tv.lineList[0]
  ;locate x: "&tv.x + 251", y: "&tv.y + 78 + 22"
  = tv.lineList[1]
  ;locate x: "&tv.x + 251", y: "&tv.y + 78 + 22 + 22"
  = tv.lineList[2]
  ;if exp: "ponkan.existSaveData(tv.saveNum)"
    ;imagebutton {
      lay:       "&tv.system.save.btnLay",
      imagefile: "image/close_button_small.png",
      x:         "&tv.x + (550 - 25)",
      y:         "&tv.y + 10",
      exp:       "&tv.deleteExp",
      jump:      true,
      label:     "on_click_delete_button"
    }
  ;endif
;endfor
;endnowait

# ページ送りボタン
---
tv.pageButtonX = 700;
tv.pageButtonY = 20;
---
;mesopt {
  fontsize: 24, lineheight: 24, color: 0xFFFFFF,
  margint: 0, marginr: 0, marginb: 0, marginl: 0
}
;locate x: "&tv.pageButtonX", y: "&tv.pageButtonY"
;nowait
PAGE:
;for loops: 10, indexvar: "i"
  ---
  tv.x = tv.pageButtonX + 80 + (tv.i * 40);
  tv.y = tv.pageButtonY;
  tv.exp = "tv.clickedPageNum = " + tv.i + ";";
  if (tv.i == tv.system.save.savePageNum) {
    tv.bgalphas = [1, 1, 1];
  } else {
    tv.bgalphas = [0.50, 0.85, 0.9];
  }
  ---
  ;textbutton {
    lay:    "&tv.system.save.btnLay",
    text:   "&'' + (tv.i + 1)",
    x:      "&tv.x",
    y:      "&tv.y",
    width:  30,
    height: 30,
    bgcolors: ["0xFFFFFF", "0xFFFFFF", "0xFFFFFF"],
    bgalphas: "&tv.bgalphas",
    exp:    "&tv.exp",
    jump:   true,
    label:  "on_click_page_button"
  }
;endfor
;endnowait

# セーブ画面の閉じるボタン
;imagebutton {
  lay:       "&tv.system.save.btnLay",
  imagefile: "image/close_button.png",
  x:         "&1280 - 30 - 24",
  y:         23,
  jump:      true,
  label:     "exit"
}


# トランジションで表示
;trans method: "crossfade", time: "&tv.system.save.transTime"
;waittrans canskip: false

*unlock_save_buttons
# セーブのボタンのロックを解除
;unlockbuttons lay: "&tv.system.save.btnLay"
# 右クリックで画面を終了
;rclick jump: true, label: "exit", enabled: true
;s



#=========================================================================================
# ページ切り替え
#=========================================================================================
*on_click_page_button
-tv.system.save.transTime = 100;
;if exp: "tv.system.save.savePageNum == tv.clickedPageNum"
  ;jump label: "unlock_save_buttons"
;else
  -tv.system.save.savePageNum = tv.clickedPageNum;
  ;jump label: "draw"
;end
;s


#=========================================================================================
# セーブする
#=========================================================================================
*on_click_save_button
-tv.dialogMessage = "No." + (tv.clickedSaveNum + 1) + " にセーブしますか？";
;call file: "script/confirm_dialog.pon", label: "show_dialog"
;if exp: "tv.dialogResult"
  ;save num: "&tv.clickedSaveNum"
  -tv.system.save.transTime = 100;
  ;jump label: "draw"
;else
  ;jump label: "unlock_save_buttons"
;end
;s


#=========================================================================================
# 削除する
#=========================================================================================
*on_click_delete_button
-tv.dialogMessage = "No." + (tv.clickedDeleteNum + 1) + " のデータを削除しますか？";
;call file: "script/confirm_dialog.pon", label: "show_dialog"
;if exp: "tv.dialogResult"
  ;deletesavedata num: "&tv.clickedDeleteNum"
  -tv.system.save.transTime = 100;
  ;jump label: "draw"
;else
  ;jump label: "unlock_save_buttons"
;end
;s


#=========================================================================================
# セーブ画面を終わる
#=========================================================================================
*exit
;tempload num: 0, sound: false, toback: true
;trans method: "crossfade", time: 200
;unlockscreenshot
;historyopt enabled: true
;return

